VAR number_of_blue_answers = 0
VAR number_of_red_answers = 0

->Chapter3
== Chapter3 ==

Это третья глава. Сегодня нужно посмотреть, все ли наши актеры читали сценарий. #mainSub
Устроим для вас небольшой экзамен! #mainSub
->Question1

=== Question1 ===
-Вопрос первый! Как зовут того, кто вас программирует? #mainSub
* [Елена?]
    ~number_of_red_answers++
    Нет, его зовут Андрей. Соберитесь. А то еще его обидим. #mainSub
    ->Question2
    
* [Андрей.]
    ~number_of_blue_answers++
    Верно! А вы меня внимательно слушали. #mainSub
    Просто это такое красивое имя, мы не могли не запомнить. #leftSub
    ... #mainSub
    Откуда у них навыки подлизывания??? #mainSub
    Ай, не важно. #mainSub
    ->Question2
    
* [Что делает?]
    ~number_of_red_answers++
    Забей, ответ уже неверный. #mainSub
    Но!.. #leftSub
    ->Question2

=== Question2 ===
-Вопрос второй! Как называется часть Вселенной, в которую вы попали? #mainSub
* [Откуда я должен это знать?]
    ~number_of_red_answers++
    Ну, предварительная подготовка за кулисами. Все такое. #mainSub
    Ладно, просто знай: это Объект 122. #mainSub
    ->Question3

* [Объект 122.]
    ~number_of_blue_answers++
    Верно! #mainSub
    ->Question3
    
* [Город доктора Моро.]
    ~number_of_red_answers++
    Что? Откуда ты про Доктора вообще знаешь? #mainSub
    Эй, ребят, тут утечка информации! #mainSub
    Правильным ответом был Объект 122. До Моро тебе еще расти и расти, блеклая моделька. #mainSub
    ->Question3
    
=== Question3 ===
Вопрос третий! Хотя, я уже не знаю, что еще спросить у обычных простыней… #mainSub
Кто из вас и есть тот самый объект 122? #mainSub
* [Это ты?]
    ~number_of_red_answers++
    Я всего лишь работник закулисья. Объект 122 - это ты. #mainSub
    ->Chapter3end
    
* [Это та вторая простынь?]
    ~number_of_red_answers++
    Я думал, мы друзья… #rightSub
    Хах, нет, он не объект 122. #mainSub
    Ты - объект 122. #mainSub
    ->Chapter3end
    
* [Это… я?]
    ~number_of_blue_answers++
    Именно! Но не спеши радоваться или огорчаться, сейчас это для тебя совершенно ничего не значит. #mainSub
    А когда будет значить, ты не вспомнишь ни этот разговор, ни меня. #mainSub
    ->Chapter3end
    

=== Chapter3end ===    
Замечательно! Теперь подведем итоги первой части теста. #mainSub
{number_of_red_answers > number_of_blue_answers:
    Вы нифига не знаете об этом мире, даже базы. Так себе, конечно. #mainSub
- else:
    Вы ответили почти на все правильно. Заслужили отдых. #mainSub
}

Ладно. Поехали в следующую главу. #mainSub

-> END